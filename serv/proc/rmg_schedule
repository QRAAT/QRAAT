#!/usr/bin/perl
#rmg_schedule
#
# This script is part of the QRAAT system.
#
# Copyright (C) 2013 Marcel Losekoot
#
# This script determines whether each site should be on or off and makes it happen.
# It also forces an update of the tx.csv file on each site that it turns on.
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

use strict ;
use warnings ;
use POSIX qw(strftime);

my $Debug = 0 ;			# Set to 1 to generate debug output
my $low_voltage = 11.5 ;        # Voltage below which system should be off
my $operational_voltage = 12.5 ; # Voltage above which system can be turned on

sub usage()
{
	print "$0: path_to_sitelist path_to_txlist [debug]\n" ;
	print "Turns sites on and off according to the schedule defined in the sitelist file\n" ;
	print "Updates the tx.csv file on each site.\n" ;
}

sub main()
{
	my $argc = scalar @ARGV ;
	if( $argc < 2 )
	{
		usage() ;
		return 1 ;
	}
	if( ($argc > 2) && ($ARGV[2] eq "debug") )
	{
		$Debug = 1 ;
	}
	my $sitelist = $ARGV[0] ;
	if( (not defined $sitelist) or ($sitelist eq "") )
	{
		print "ERROR: missing parameter for sitelist\n" ;
		return 1 ;
	}
	print "$0: reading sitelist from '$sitelist'\n" ;
	my $rmg_csv = `rmg_csv -c name <$sitelist` ;
	my @sitenames = split(/\n/,$rmg_csv) ;
	if( $Debug ) { print "debug: main: sitenames=@sitenames\n" ; }
	if( scalar @sitenames < 1 )
	{
		print "ERROR: cannot read sitelist\n" ;
		return 1 ;
	}
	my $txlist = $ARGV[1] ;
	if( (not defined $txlist) or ($txlist eq "") )
	{
		print "ERROR: missing parameter for txlist\n" ;
		return 1 ;
	}
	print "$0: reading txlist from '$txlist'\n" ;
	#
	# Loop through each site in the sitenames array
	my $linecount = 0 ;
	for my $sitename ( @sitenames )
	{
		$linecount++ ;
		if( $Debug ) { print "debug: main: sitename=$sitename\n" ; }
		if( (not defined $sitename) or ($sitename eq "") ) { next ; }
		#name,comp_ip,power_ip,comp_outlet,rx_outlet,powertype,state,auto,schedule
		my $comp_ip = `rmg_csv $sitename comp_ip <$sitelist` ;
		chomp($comp_ip) ;
		my $power_ip = `rmg_csv $sitename power_ip <$sitelist` ;
		chomp($power_ip) ;
    my $power_type = `rmg_csv $sitename powertype <$sitelist` ;
    chomp($power_type) ;
		my $automatic = `rmg_csv $sitename auto <$sitelist` ;
		chomp($automatic) ;
		my $schedule = `rmg_csv $sitename schedule <$sitelist` ;
		chomp($schedule) ;
		if( $Debug ) { print "debug: main: site=$sitename comp_ip=$comp_ip power_ip=$power_ip auto=$automatic schedule=$schedule\n" ; }
		if( (not defined $comp_ip) or ($comp_ip eq "") or ($comp_ip eq "0.0.0.0") )
		{
			print "No computer ip for site $sitename\n" ;
			next ;
		}
		if( (defined $automatic) and ($automatic ne "auto") )
		{
			print "Automatic schedule is disabled for site $sitename, set auto column to 'auto' to enable it\n" ;
			next ;
		}
    if ( $power_type ne "pingbrother" )
    {
      $power_ip = "" ;
    }
		onoff($sitename,$schedule,$txlist,$power_ip) ;
	}	
	#
	return 0 ;
}

sub get_rmg_status($)
{
	my ( $sitename ) = @_ ;
	my $ping_power = "" ;
	my $ping_computer = "" ;
	my $site_status = "" ;
	if( (defined $sitename) and ($sitename ne "") )
	{
		my $command = "rmg status $sitename" ;
		if( $Debug ) { print "debug: get_rmg_status: command=$command\n" ; }
		my $result = `$command` ;
		if( $Debug ) { print "debug: get_rmg_status: result=$result\n" ; }
		my @lines = split /\r|\n/,$result ;
		if( $lines[0] =~ m/^error/ )
		{
			print "Error: bad response from rmg status command: $result\n" ;
		}
		else
		{
			my $line = shift @lines ;	# drop the line with "status siteX"
			$line = shift @lines ;	# grab the line with "Powerswitch Router Ping Test:"
			my ( $status1 ) = ($line =~ m/.+?:\s*?(\w+)/) ;
			$line = shift @lines ;	# grab the line with "Computer Ping Test:"
			my ( $status2 ) = ($line =~ m/.+?:\s*?(\w+)/) ;
			$line = shift @lines ;	# grab the line with "Site Status:"
			my ( $status3 ) = ($line =~ m/.*?:\s*([^,]*),/) ;
			if( defined $status1 )
			{
				if( $status1 eq "failure" ) { $ping_power = 0 ; }	# 0 means failure
				elsif( $status1 eq "success" ) { $ping_power = 1 ; }	# 1 means success
				else { $ping_power = 0 ; }
				if( $Debug ) { print "debug: get_rmg_status: status1=$status1 ping_power=$ping_power\n" ; }
			}
			if( defined $status2 )
			{
				if( $status2 eq "failure" ) { $ping_computer = 0 ; }	# 0 means failure
				elsif( $status2 eq "success" ) { $ping_computer = 1 ; }	# 1 means success
				else { $ping_computer = 0 ; }
				if( $Debug ) { print "debug: get_rmg_status: status2=$status2 ping_computer=$ping_computer\n" ; }
			}
			if( defined $status3 )
			{
				if( $status3 eq "unknown" ) { $site_status = 0 ; }			# 0 means unknown
				elsif( $status3 eq "off" ) { $site_status = 1 ; }				# 1 means off
				elsif( $status3 eq "down" ) { $site_status = 3 ; }				# 3 means down (off)
				elsif( $status3 eq "Site stopped; is now up" ) { $site_status = 4 ; }	# 4 means on not running
				elsif( $status3 eq "Site set to go down in 300 seconds" ) { $site_status = 4 ; }	# 4 means on not running
				elsif( $status3 eq "Site is now up" ) { $site_status = 4 ; }		# 4 means on not running
				elsif( $status3 eq "Site started" ) { $site_status = 5 ; }			# 5 means running RMG
				elsif( $status3 eq "Site restarted" ) { $site_status = 5 ; }		# 5 means running RMG ?
				else { $site_status = 2 ; }						# 2 means not off
				#
				if( $Debug ) { print "debug: get_rmg_status: status3=$status3 site_status=$site_status\n" ; }
			}
		}
	}
	else
	{
		print "Error: missing sitename in get_rmg_status\n" ;
	}
	return ( $ping_power, $ping_computer, $site_status ) ;
}

sub get_pingbrother_status($$)
{
	my ( $sitename, $ip ) = @_ ;
	my $command = "curl --connect-timeout 10 -s -u admin:admin http://$ip" ;
	#my $result = `cat pbtest` ;	# test
	my $result = `$command` ;
	$result =~ s/\r//g ;
	$result =~ s/\n//g ;
	#<tr><td><b>Input Voltage:</b></td><td></td><td>13.69V</td></tr>
	my ( $intemp ) = ( $result =~ /.*Internal Temp.+?>(\d{1,2}\.\d{1,2}).C<.*/ ) ;
	#<tr><td><b>Internal Temp:</b></td><td></td><td>39.8°C</td></tr>
	my ( $extemp ) = ( $result =~ /.*External Temp.+?>(\d{1,2}\.\d{1,2}).C<.*/ ) ;
	#<tr><td><b>External Temp:</b></td><td></td><td>35.7°C</td></tr>
	my ( $voltage ) = ( $result =~ /.*Input Voltage.+?>(\d{1,2}\.\d{1,2})V<.*/ ) ;
	#<td><b>&nbsp;&nbsp;&nbsp;&nbsp;Channel 1:</b></td>
	#<td>ON,&nbsp;&nbsp;&nbsp;&nbsp;Current:0.0A, Power:0.0W</td>
	my ( $current1 ) = ( $result =~ /.*Channel 1.+?Current:(\d{1,2}\.\d{1,2})A.*/ ) ;
	my ( $power1 ) = ( $result =~ /.*Channel 1.+?Power:(\d{1,2}\.\d{0,2})W.*/ ) ;
	my ( $current2 ) = ( $result =~ /.*Channel 2.+?Current:(\d{1,2}\.\d{0,2})A.*/ ) ;
	my ( $power2 ) = ( $result =~ /.*Channel 2.+?Power:(\d{1,2}\.\d{0,2})W.*/ ) ;
	if( not defined $intemp ) { $intemp = "" ; }
	if( not defined $extemp ) { $extemp = "" ; }
	if( not defined $voltage ) { $voltage = "" ; }
	if( not defined $current1 ) { $current1 = "" ; }
	if( $current1 eq "" ) { $current1 = "0" ; }
	if( not defined $power1 ) { $power1 = "" ; }
	if( $power1 eq "" ) { $power1 = "0" ; }
	if( not defined $current2 ) { $current2 = "" ; }
	if( $current2 eq "" ) { $current2 = "0" ; }
	if( not defined $power2 ) { $power2 = "" ; }
	if( $power2 eq "" ) { $power2 = "0" ; }
	my $current = $current1 + $current2 ;
	my $power = $power1 + $power2 ;
	if( $Debug ) { print "debug: get_pingbrother_status: $sitename at $ip: intemp=$intemp extemp=$extemp voltage=$voltage current1=$current1 power1=$power1 current2=$current2 power2=$power2 current=$current power=$power\n" ; }
	if( ($intemp eq "") and ($extemp eq "") and ($voltage eq "") )
	{
		print "No valid telemetry read from '$sitename' at IP '$ip'\n" ;
	}
	#return ( $intemp, $extemp, $voltage, $current1, $power1, $current2, $power2, $current, $power ) ;
	return ( $intemp, $extemp, $voltage ) ;
}

sub onoff($$$$)
{
	my ( $sitename, $schedule, $txlist, $power_ip ) = @_ ;
	if( $Debug ) { print "debug: onoff: $sitename $schedule $txlist\n" ; }
	if( (not defined $schedule) || ($schedule eq ""))
	{
		print "$sitename has no schedule\n " ;
		return 1 ;
	}
	my $nowsec = time() ;
	my $nowtime = strftime("%H:%M",localtime($nowsec)) ;
	my @parts = split /\|/,$schedule ;
	my $count = scalar @parts ;
	if( $Debug ) { print "debug: onoff: $sitename schedule has $count part(s)\n" ; }
	my $action = 0 ;	# assume the site should be off, unless the schedule says otherwise
	for my $part ( @parts )
	{
		my @times = split /-/,$part ;
		$count = scalar @times ;
		if( $count != 2 )
		{
			print "error: $sitename has badly formatted schedule part: '$part'\n" ;
			print "Format for schedule is start-stop|start-stop... where start and stop are HH:MM\n" ;
			return 1 ;
		}
		my $starttime = $times[0] ;
		my $stoptime = $times[1] ;
		my ( $starthour, $startmin ) = ($starttime =~ m/(\d{1,2}):(\d{2})/) ;
		my ( $stophour, $stopmin ) = ($stoptime =~ m/(\d{1,2}):(\d{2})/) ;
		$starthour = substr("0".$starthour,-2,2) ;
		$startmin = substr("0".$startmin,-2,2) ;
		$stophour = substr("0".$stophour,-2,2) ;
		$stopmin = substr("0".$stopmin,-2,2) ;
		$starttime = "$starthour:$startmin" ;
		$stoptime = "$stophour:$stopmin" ;
		if( $starttime eq $stoptime )
		{
			print "error: $sitename schedule may not have start time equal to stop time: $part\n" ;
			print "To disable the on/off schedule, use 00:00-24:00\n" ;
			return 1 ;
		}
		if( $Debug ) { print "debug: onoff: start $starthour:$startmin stop $stophour:$stopmin\n" ; }
		if( $starttime gt $stoptime ) # On over midnight, split the comparison
		{
			if( ($nowtime ge $starttime) || ($nowtime le $stoptime) )
			{
				$action = 1 ;	# On
			}
		}
		else
		{
			if( ($nowtime ge $starttime) && ($nowtime le $stoptime) )
			{
				$action = 1 ;	# On
			}
		}
	}
	if( $action )
	{
		print "$sitename schedule is '$schedule', timenow is $nowtime, site should be on\n" ;
	}
	else
	{
		print "$sitename schedule is '$schedule', timenow is $nowtime, site should be off\n" ;
	}
	my $err = nudge($sitename,$action,$txlist,$power_ip) ;
	if( $Debug ) { print "debug: onoff: nudge $sitename $action returned $err\n" ; }
	if( $err )
	{
		print "error: cannot control $sitename\n" ;
	}
}

sub nudge($$$$)	# nudges the site from its current state towards action. returns 0 for OK 1 for error
{
	my ( $sitename, $action, $txlist, $power_ip ) = @_ ;
	if( $Debug ) { print "debug: nudge: $sitename action=$action txlist=$txlist\n" ; }
	my ( $ping_power, $ping_computer, $site_status ) = get_rmg_status($sitename) ;
	# Quick sanity check to make sure this is going to work
	if( $Debug ) { print "debug: nudge: ping_power=$ping_power ping_computer=$ping_computer site_status=$site_status\n" ; }
	if( (not $ping_computer) && (not $ping_power) )
	{
		if( $Debug ) { print "debug: nudge: abandon $sitename\n" ; }
		return 1 ;
	}
  my ( $intemp, $extemp, $voltage ) ;
  if ( $power_ip ne "") 
  {
    ( $intemp, $extemp, $voltage ) = get_pingbrother_status($sitename,$power_ip) ;
    if ( $Debug ) { print "debug: nudge: $sitename; intemp=$intemp, extemp=$extemp, voltage=$voltage\n" ; }
  }
	#
	# Here's all the ways you can try to turn ON a site
	#
	if( $action )#Want to turn site ON
  {
		if( $ping_computer )#computer is up and responding
		{
      if ( ($power_ip ne "") and ($voltage < $low_voltage) )#low voltage condition
      {
        if ( $Debug ) { print "debug: nudge: low power at $sitename\n" ; }
        if ( ! do_rmg_down_now($sitename) )
        {
          sleep(5) ;
          return do_rmg_off($sitename) ;
        }
        else
        {
          return 1 ;
        }
      }
      else
      {
			  # transfer the tx.csv file every time
			  if( $Debug ) { print "debug: nudge: updating $txlist for $sitename\n" ; }
			  if( do_rmg_updatetx($sitename,$txlist) )
			  {
				  print "ERROR: cannot update tx list for $sitename\n" ;
			  }
			  # computer up, rmg running: do nothing
			  if( ($site_status == 5) )
			  {
				  print "$sitename is active\n" ;
				  if( $Debug ) { print "debug: nudge: do nothing $sitename\n" ; }
				  return 0 ;
			  }
			  # computer up, rmg stopped: start the rmg
			  elsif( ($site_status == 4) )
			  {
				  print "$sitename is up\n" ;
				  if( $Debug ) { print "debug: nudge: rmg start $sitename\n" ; }
				  return do_rmg_start($sitename) ;
			  }
			  # computer up, other state: start the rmg
			  else
			  {
				  print "$sitename is up\n" ;
				  if( $Debug ) { print "debug: nudge: rmg start $sitename\n" ; }
				  return do_rmg_start($sitename) ;
			  }
      }
    }
    elsif( $ping_power )#computer down, powerswitch responding
		{
			# computer off, power switch present, turn on the computer
			if( ($site_status == 1) and ($power_ip ne "") and ($voltage > $operational_voltage) )
			{
				print "$sitename is off\n" ;
				if( $Debug ) { print "debug: nudge: rmg on $sitename\n" ; }
				if ( ! do_rmg_on($sitename) )#returns 0 if successful
				{
					sleep(30) ;
					if( $Debug ) { print "debug: nudge: rmg start $sitename\n" ; }
					return do_rmg_start($sitename) ;
				}
				else
				{
					return 1 ;
				}
			}
			# abnormal state, power switch present: turn off the computer
			elsif ( ($power_ip ne "") and ($voltage < $operational_voltage) )
			{
				print "$sitename is off but has low voltage\n" ;
				if ( $Debug ) { print "debug: nudge: do nothing $sitename\n" ; }
				return 0 ;
			}
			else
			{
				print "$sitename is in an abnormal state\n" ;
				if( $Debug ) { print "debug: nudge: rmg die $sitename\n" ; }
				return do_rmg_die($sitename) ;
			}
		}
        }#end turn ON
	#
	# Here's all the ways you can try to turn OFF a site
	#
    else#Want to turn site OFF
    {
		# computer off: do nothing
		if( ($site_status == 1) )
		{
			print "$sitename is off\n" ;
			if( $Debug ) { print "debug: nudge: do nothing $sitename\n" ; }
			return 0 ;
		}
		elsif( $ping_computer )#computer is up and responding
		{
			# computer responding, power switch present: shutdown the computer 
			if( $ping_power )#powerswitch is responding
			{
				print "$sitename is up\n" ;
				if( $Debug ) { print "debug: nudge: rmg down_now $sitename\n" ; }
				if ( ! do_rmg_down_now($sitename) )
				{
					sleep(5) ;
					return do_rmg_off($sitename) ;
				}
				else
				{
					return 1 ;
				}
			}
			# computer responding but not in up state, power switch not present: put computer in up state
			elsif ( ($site_status != 4) )
			{
				print "$sitename is not up\n" ;
				if( $Debug ) { print "debug: nudge: rmg stop $sitename\n" ; }
				return do_rmg_stop($sitename) ;
			}
		}
		elsif( $ping_power )#computer not responding, powerswitch responding
		{
			# computer down and power switch present: turn off the computer
			if( ($site_status == 3))
			{
				print "$sitename is down\n" ;
				if( $Debug ) { print "debug: nudge: rmg off $sitename\n" ; }
				return do_rmg_off($sitename) ;
			}
			# abnormal state, power switch present: turn off the computer
			else
			{
				print "$sitename is in an abnormal state\n" ;
				if( $Debug ) { print "debug: nudge: rmg die $sitename\n" ; }
				return do_rmg_die($sitename) ;
			}
		}
        }#end turn OFF
	print "error: $sitename unexpected case: action=$action site_status=$site_status ping_computer=$ping_computer ping_power=$ping_power\n" ;
	return 1 ;
}

sub do_rmg_start($)
{
	my ( $sitename ) = @_ ;
	return do_rmg($sitename,"start") ;
}

sub do_rmg_stop($)
{
	my ( $sitename ) = @_ ;
	return do_rmg($sitename,"stop") ;
}

sub do_rmg_down($)
{
	my ( $sitename ) = @_ ;
	return do_rmg($sitename,"down") ;
}

sub do_rmg_die($)
{
	my ( $sitename ) = @_ ;
	return do_rmg($sitename,"die") ;
}

sub do_rmg_down_now($)
{
	my ( $sitename ) = @_ ;
	return do_rmg($sitename,"down_now") ;
}

sub do_rmg_on($)
{
	my ( $sitename ) = @_ ;
	return do_rmg($sitename,"on") ;
}

sub do_rmg_off($)
{
	my ( $sitename ) = @_ ;
	return do_rmg($sitename,"off") ;
}

sub do_rmg_updatetx($$)
{
	my ( $sitename, $txlist ) = @_ ;
	return do_rmg($sitename,"updatetx --file $txlist") ;
}

sub do_rmg($$)
{
	my ( $sitename, $mode ) = @_ ;
	if( not defined $mode ) { return 1 ; }
	if( $mode eq "" ) { return 1 ; }
	my $command = "rmg $mode $sitename" ;
	#my $command = "echo rmg $mode $sitename" ;
	print "Sending command: $command\n" ;
	if( $Debug ) { print "debug: do_rmg: command=$command\n" ; }
	my $result = `$command` ;
	chomp($result) ;
	if( $Debug )
	{
		print "debug: do_rmg: result=$result\n" ;
		sleep(10) ;
		my ( $ping_power, $ping_computer, $site_status ) = get_rmg_status($sitename) ;
		print "debug: do_rmg: ping_power $ping_power ping_computer $ping_computer site_status $site_status\n" ;
	}
	return 0 ;
}


exit main() ;

#END
